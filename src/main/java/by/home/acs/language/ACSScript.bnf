{
  parserClass="by.home.acs.language.parser.ACSScriptParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="ACSScript"
  psiImplClassSuffix="Impl"
  psiPackage="by.home.acs.language.psi"
  psiImplPackage="by.home.acs.language.psi.impl"

  elementTypeHolderClass="by.home.acs.language.ACSScriptTypes"
  elementTypeClass="by.home.acs.language.psi.ACSScriptElementType"
  tokenTypeClass="by.home.acs.language.psi.ACSScriptTokenType"

  psiImplUtilClass="by.home.acs.language.psi.impl.ACSScriptPsiImplUtil"
  generate=[tokens="yes"]

  tokens = [
          FUNCTION='function'
                  SCRIPT='script'
                  WHITE_SPACE = 'regexp:\s+'
                  NUMBER='regexp:(-?[0-9]+)'
                  STRING = 'regexp:\"([^\\\"]|\\.)*\"'
                  VOID = 'void'
                  INT = 'int'
                  STR = 'str'
                  BOOl = 'bool'
                  COMMA = ','
                  IDENTIFIER = 'regexp:[a-zA-Z_][a-zA-Z0-9_]*'
      ]
}


ACSScriptFile ::= FileContainer

private FileContainer ::= (COMMENT|Definition*)

Definition ::= (FunctionDefinition | ScriptDefinition | DirectivesDeclaration | GlobalModifier | VariableDefinition)
 {  mixin="by.home.acs.language.psi.impl.ACSNamedElementImpl"
    implements="by.home.acs.language.psi.ACSNamedElement"
    methods=[getName setName getNameIdentifier getNameIdentifier getFunctionKeyword getFunctionStatementReturnType]
 }


FunctionDefinition ::= FunctionIdentifier FunctionReturnType FunctionNameAndParams FunctionBody

ScriptDefinition ::= ScriptIdentifier ScriptName (ScriptPossibleParams | ScriptType) NetType? ScriptBody
/**
    Directives statement
 */
DirectivesDeclaration ::= (IncludeDeclaration | ImportDeclaration | DefineDeclaration | LibraryDeclaration)

IncludeDeclaration ::= PoundSymbol include STRING

ImportDeclaration ::= PoundSymbol import STRING

DefineDeclaration ::= PoundSymbol define IDENTIFIER  IDENTIFIER (Operator (IDENTIFIER | NUMBER))?

LibraryDeclaration ::= PoundSymbol library STRING

/**
    Script statements
 */
ScriptIdentifier ::= SCRIPT

ScriptType ::= OPEN | ENTER | RETURN | RESPAWN | DEATH | LIGHTNING | UNLOADING | DISCONNECT | KILL | REOPEN

ScriptName ::= (STRING | NUMBER)

ScriptPossibleParams ::= OpenBracket (ScriptParameterWithVoid | ScriptParameter) CloseBracket

ScriptParameterWithVoid ::=  VoidType

ScriptParameter ::= ScriptParameterList

ScriptParameterList ::= ScriptFormalParameter (CommaSymbol ScriptFormalParameter)*

ScriptFormalParameter ::= IntType IDENTIFIER

ScriptBody ::= OpenBrace ScriptStatement* CloseBrace

/**
    Function statements
 */
FunctionIdentifier ::= FUNCTION

FunctionInvocation ::= IDENTIFIER OpenBracket CloseBracket Semicolon ReturnOperator?

FunctionReturnType ::= VoidType | Type

FunctionParameterWithVoid ::= VoidType

FunctionNameAndParams ::= IDENTIFIER OpenBracket FunctionParameter CloseBracket

FunctionParameter ::=  (FunctionParameterWithVoid | FunctionParameterList)

FunctionParameterList ::= FunctionFormalParameter (CommaSymbol FunctionFormalParameter)*

FunctionFormalParameter ::= Type IDENTIFIER

FunctionBody ::= OpenBrace Statement* CloseBrace

/**

 */

GlobalModifier ::= (global | world) Type IDENTIFIER Colon IDENTIFIER Semicolon

StaticModifier ::= static

NetType ::= NET

VoidType ::= VOID

TerminateKeyword ::= terminate

Statement ::= (VariableDefinition | AssignmentStatement | FunctionInvocation | ControlFlowStatement | Loops | ReturnOperator)

ScriptStatement ::= VariableDefinition | VariableInitialization | AssignmentStatement | FunctionInvocation | ControlFlowStatement | Loops | (TerminateKeyword Semicolon)

AssignmentStatement ::= IDENTIFIER Equals (FunctionInvocation)

VariableDefinition ::= StaticModifier? Type VariableName ArrayType? (Semicolon | (Equals (FunctionInvocation | VariableInitialization Semicolon)))

left VariableName ::=  IDENTIFIER  (CommaSymbol VariableName)*

VariableInitialization ::= AllTypes (CommaSymbol VariableName Equals AllTypes)*

Operator ::= (AssignmentOperator | RelationalOperator | ArithmeticOperator | LogicalOperator | BitwiseOperator)

AssignmentOperator ::= ('=' | '*=' | '/=' | '%=' | '+=' | '-=' | '<<=' | '>>=' | '&=' | '^=' | '|=')

RelationalOperator ::= ('>' | '<' | '==' | '!=' | '>=' | '<=')

ArithmeticOperator ::= ( '++' | '--' | '+' | '-' | '/' | '%' | '*' )

LogicalOperator ::= ('&&' | '||' | '!')

BitwiseOperator ::= ('&' | '|' | '^' | '~' | '<<' | '>>')

Loops ::= ForLoop | whileLoop | untilLoop | doWhileLoop | doUntilLoop

ForLoop ::= for OpenBracket Initialization Condition Iteration CloseBracket OpenBrace ((LoopStatement* BreakStatement) | (LoopStatement* ContinueStatement) | LoopStatement*) CloseBrace

private Initialization ::= EmptyInitialization | TypeInitialization

EmptyInitialization ::= Semicolon

TypeInitialization ::= StaticModifier? Type VariableList  ArithmeticInitialization* Semicolon

private VariableList ::= VariableName Equals (NUMBER | STRING | IDENTIFIER | LogicalType) (CommaSymbol VariableList)?//check logical type

private ArithmeticInitialization ::= ArithmeticOperator (NUMBER | STRING | IDENTIFIER)

private Condition ::= EmptyCondition | NormalCondition | ArithmeticCondition

private LogicalCondition ::= RelationalOperator

NormalCondition ::= ComparisonCondition (LogicalCondition ComparisonCondition)? Semicolon

private ComparisonCondition ::= IDENTIFIER LogicalCondition (IDENTIFIER | NUMBER | STRING | LogicalType (LogicalCondition NormalCondition )?)

private ArithmeticCondition ::= NormalCondition LogicalOperator

EmptyCondition ::= Semicolon

private Iteration ::=  NormalIteration | EmptyIteration

EmptyIteration ::= //not sure with this...

NormalIteration ::=
 IDENTIFIER ArithmeticOperator | ArithmeticOperator IDENTIFIER | IDENTIFIER AssignmentOperator (NUMBER | STRING | LogicalType | IDENTIFIER)

whileLoop ::= while OpenBracket LogicalType CloseBracket OpenBrace Statement* CloseBrace

untilLoop ::= until OpenBracket LogicalType CloseBracket OpenBrace Statement* CloseBrace

doWhileLoop ::= do OpenBrace Statement* CloseBrace while OpenBracket LogicalType CloseBracket Semicolon

doUntilLoop ::= do OpenBrace Statement* CloseBrace until OpenBracket LogicalType CloseBracket Semicolon

LoopStatement ::= VariableDefinition | VariableInitialization | AssignmentStatement | FunctionInvocation | LoopControlFlowStatement | Loops

LoopControlFlowStatement ::= LoopIfThenElseStatement | LoopIfThenStatement | Loops | SwitchLoop

LoopIfThenStatement ::= if OpenBracket LogicalType CloseBracket OpenBrace ((LoopStatement* BreakStatement) | (LoopStatement* ContinueStatement) | LoopStatement*) CloseBrace

LoopIfThenElseStatement ::= IfThenStatement else (OpenBrace ((LoopStatement* BreakStatement) | (LoopStatement* ContinueStatement) | LoopStatement*) CloseBrace | IfThenElseStatement?)

Type ::= (IntType | StringType | BoolType)

private AllTypes ::= STRING | CHARACTER | EmptyString | LogicalType | ArrayInitializer | NUMBER | FLOAT

BoolType ::= BOOL

IntType ::= INT

StringType ::= STR

EmptyString ::= '""'

ArrayType ::=  OpenSquareBracket NUM CloseSquareBracket

ArrayInitializer ::= OpenBrace ArrayInitializers? CloseBrace

ArrayInitializers ::= (NUMBER | STRING) (CommaSymbol ArrayInitializers)?

LogicalType ::= true | false

ReturnOperator ::= return Semicolon | return IDENTIFIER Semicolon // Use something else but IDENTIFIER later

ControlFlowStatement ::= IfThenElseStatement | IfThenStatement |  SwitchLoop

IfThenStatement ::= if OpenBracket LogicalType CloseBracket OpenBrace Statement* CloseBrace

IfThenElseStatement ::= IfThenStatement else (OpenBrace Statement* CloseBrace | IfThenElseStatement?)

SwitchLoop ::= switch OpenBracket (IDENTIFIER | NUMBER) CloseBracket OpenBrace CaseOperator CloseBrace

CaseOperator ::=   CaseIdentifier ((SwitchStatement*  BreakStatement) | SwitchStatement*)  (default Colon SwitchStatement*)? CaseOperator?

private CaseIdentifier ::= case (IDENTIFIER | NUMBER | STRING) Colon

SwitchStatement ::= VariableDefinition | VariableInitialization | AssignmentStatement | FunctionInvocation | SwitchControlFlowStatement | Loops

SwitchControlFlowStatement ::= BreakableIfThenElseStatement | BreakableIfThenStatement | SwitchLoop

BreakableIfThenStatement ::= if OpenBracket LogicalType CloseBracket OpenBrace SwitchStatement* BreakStatement? CloseBrace

BreakableIfThenElseStatement ::= BreakableIfThenStatement else (OpenBrace Statement* CloseBrace | BreakableIfThenElseStatement?)

/*
    Key symbol's
 */

Equals ::= '='

Dot ::= '.' //not used

CommaSymbol ::= COMMA

OpenBrace ::= '{'

CloseBrace ::= '}'

OpenBracket ::= '('

CloseBracket ::= ')'

PoundSymbol ::= '#'

Semicolon ::= ';'

Colon ::= ':'

OpenSquareBracket ::= '['

CloseSquareBracket ::= ']'

NUM ::= NUMBER

/**
    Loop Statements
 */

BreakStatement ::= break Semicolon

ContinueStatement ::= continue Semicolon